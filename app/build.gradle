apply plugin: 'com.android.application'

android {
    compileSdkVersion 20
    buildToolsVersion "23.0.0"

    defaultConfig {
        applicationId "edu.umich.eecs.androidlibsvm"
        minSdkVersion 15
        targetSdkVersion 20
        versionCode 1
        versionName "1.0"
        ndk {
            moduleName "jnilibsvm" // <-- This is the name of my C++ module!
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets.main {
		jniLibs.srcDir 'src/main/libs'
		jni.srcDirs = []
    }

    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = android.ndkDirectory
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath, // Change src/main/jni the relative path to your jni source
                '-j', Runtime.runtime.availableProcessors(),
                'all',
                'NDK_DEBUG=1'
    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        def ndkDir = android.ndkDirectory
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath, // Change src/main/jni the relative path to your jni source
                'clean'
    }

    clean.dependsOn 'cleanNative'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
